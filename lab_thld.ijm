//Choose directories and create a list of files
inputFolder=getDirectory("Choose input folder");
outputFolder=getDirectory("Choose output folder for the results");
list=getFileList(inputFolder);

//Create an option dialog box
Dialog.create("Options");
Dialog.addNumber("Distance in pixels", 2.85);
Dialog.addNumber("Known distance", 1);
Dialog.addNumber("Minimum area (unit"+fromCharCode(0x00B2)+")", 10);
Dialog.addCheckbox("Activate Watershed", false);
Dialog.show();
disPix = Dialog.getNumber();
disKnown = Dialog.getNumber();
minArea = Dialog.getNumber();
watershed = Dialog.getCheckbox();

//Set a watershed label for the output names
watershedLabel = ""; 
if(watershed!=false) watershedLabel="_ws";

//Choose what you need to measure
run("Set Measurements...", "area mean min redirect=None decimal=4");
run("Clear Results");

//Processing loop of the images
for(i=0; i<list.length; i++)
{
	//Open image
	imgPath=inputFolder+list[i];
	open(imgPath);
	//Setup outup path
	outputPath=outputFolder+list[i];
	fileExtension=lastIndexOf(outputPath,"."); 
	if(fileExtension!=-1) outputPath=substring(outputPath,0,fileExtension);
	//Set scale according to user inputs
	run("Set Scale...", "distance="+ disPix+ " known="+ disKnown);
	if(nImages>=1)
	{
		currentNResults = nResults; //Save the numer of the last result
		getRoi(); //Add all the particles to the ROI manager
		selectWindow(list[i]);
		getBBolean(); //Creates a mask with LAB-b* channel (green particles)
		getMes(); //Measure the values of all particles and creates green/red overlay
		//Add the file name in each row 
		for (row = currentNResults; row < nResults; row++)
		{
			setResult("Image", row, list[i]);
		}
		//Transfer the ROI overlay to the original image and save
		selectWindow(list[i]+"_ori");
		roiManager("Show All without labels"); 
		run("Flatten");
		saveAs("Tiff", outputPath+watershedLabel+".tiff");
		roiManager("Delete"); //Clear the ROI manager
		close("*"); //Close all images
	}
	showProgress(i, list.length);  //Shows a progress bar 
}
saveAs("results", outputFolder+ "results.csv"); //Save results
selectWindow("Results"); run("Close"); 
selectWindow("ROI Manager"); run("Close"); 

function getRoi()
{
	run("Duplicate...", " ");
	rename(list[i]+"_ori");
	run("Duplicate...", " ");
	run("8-bit");
	run("Gaussian Blur...", "sigma=2"); //Blur the particles to be sure to select the objects and not the sub-objects
	setAutoThreshold("Default");
	run("Convert to Mask");
	if(watershed!=false) run("Watershed");
	run("Fill Holes");
	run("Analyze Particles...","size="+minArea+"-Infinity add");
}

function getBBolean()
{
	// Color Thresholder 2.0.0-rc-69/1.52p
	// Autogenerated macro, single images only!
	min=newArray(3);
	max=newArray(3);
	filter=newArray(3);
	a=getTitle();
	call("ij.plugin.frame.ColorThresholder.RGBtoLab");
	run("RGB Stack");
	run("Gaussian Blur...", "sigma=2"); //Blur the particles to be sure to select the objects and not the sub-objects
	run("Convert Stack to Images");
	selectWindow("Red");
	rename("0");
	selectWindow("Green");
	rename("1");
	selectWindow("Blue");
	rename("2");
	min[0]=0;
	max[0]=170;
	filter[0]="pass";
	min[1]=0;
	max[1]=255;
	filter[1]="pass";
	min[2]=140;
	max[2]=255;
	filter[2]="pass";
	for (i=0;i<3;i++){
	  selectWindow(""+i);
	  setThreshold(min[i], max[i]);
	  run("Convert to Mask");
	  if (filter[i]=="stop")  run("Invert");
	}
	imageCalculator("AND create", "0","1");
	imageCalculator("AND create", "Result of 0","2");
	for (i=0;i<3;i++){
	  selectWindow(""+i);
	  close();
	}
	selectWindow("Result of 0");
	close();
	selectWindow("Result of Result of 0");
	rename(a);
}

function getMes()
{
	roiManager("Measure");
	roiManager("Deselect");
	roiManager("Set Color", "Red");  
	for (iRow = 0; iRow < nResults-currentNResults; iRow++)
	{
		meanParticle=getResult("Mean", iRow+currentNResults);
		if (meanParticle>30)
		{
			roiManager("Select", iRow); 
			roiManager("Set Color", "Green"); 
		}
	}
	roiManager("Show All without labels"); 
	run("Flatten");
	saveAs("Tiff", outputPath+watershedLabel+"_LAB_b.tiff");
}